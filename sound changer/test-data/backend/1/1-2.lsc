feature_def
    switch type(consonant, vowel) root

    feature +voice consonant
    switch manner(plosive, nasal, fricative, approximant, affricate, trill, tap) consonant
    switch place(bilabial, labiodental, dental, alveolar, postalveolar, retroflex, palatal, velar, uvular, pharyngeal, glottal) consonant
    feature +lateral (fricative, approximant)
    feature +ejective consonant
    feature +syllabic consonant

    feature +aspirated consonant!glottal
    feature +labialized consonant!bilabial
    feature +velarized consonant!velar
    feature +palatalized consonant!palatal
    feature +pharyngealized consonant!pharyngeal

    feature +nasal-release plosive

    feature +round vowel
    feature closeness(close, close-mid, mid-close, open-mid, open) vowel
    feature backness(front, mid-back, back) vowel
    feature +rhoticized vowel

    feature +long all
    feature +nasal all
    feature +breathy all

    feature +non-syllabic all
    feature +stress vowel
end

symbols
    symbol i [close front -round]
    symbol y [close front +round]
    symbol ɨ [close mid-back -round]
    symbol ʉ [close mid-back +round]
    symbol ɯ [close back -round]
    symbol u [close back +round]

    symbol e [close-mid front -round]
    symbol ø [close-mid front +round]
    symbol ɘ [close-mid mid-back -round]
    symbol ɵ [close-mid mid-back +round]
    symbol ɤ [close-mid back -round]
    symbol o [close-mid back +round]

    symbol ə [mid-close mid-back]

    symbol ɛ [open-mid front -round]
    symbol œ [open-mid front +round]
    symbol ɜ [open-mid mid-back -round]
    symbol ɞ [open-mid mid-back +round]
    symbol ʌ [open-mid back -round]
    symbol ɔ [open-mid back +round]

    symbol a [open front -round]
    symbol ɶ [open front +round]
    symbol ɑ [open back -round]
    symbol ɒ [open back +round]


    symbol p [-voice bilabial plosive]
    symbol b [+voice bilabial plosive]
    symbol t [-voice alveolar plosive]
    symbol d [+voice alveolar plosive]
    symbol ʈ [-voice retroflex plosive]
    symbol ɖ [+voice retroflex plosive]
    symbol c [-voice palatal plosive]
    symbol ɟ [+voice palatal plosive]
    symbol k [-voice velar plosive]
    symbol g [+voice velar plosive]
    symbol q [-voice uvular plosive]
    symbol ɢ [+voice uvular plosive]
    symbol ʔ [-voice glottal plosive]

    symbol ɸ [-voice bilabial fricative]
    symbol β [+voice bilabial fricative]
    symbol f [-voice labiodental fricative]
    symbol v [+voice labiodental fricative]
    symbol θ [-voice dental fricative]
    symbol ð [+voice dental fricative]
    symbol s [-voice alveolar fricative]
    symbol ɬ [-voice alveolar fricative +lateral]
    symbol z [+voice alveolar fricative]
    symbol ɮ [+voice alveolar fricative +lateral]
    symbol ʃ [-voice postalveolar fricative]
    symbol ʒ [+voice postalveolar fricative]
    symbol ʂ [-voice retroflex fricative]
    symbol ʐ [+voice retroflex fricative]
    symbol ç [-voice palatal fricative]
    symbol ʝ [+voice palatal fricative]
    symbol x [-voice velar fricative]
    symbol ɣ [+voice velar fricative]
    symbol χ [-voice uvular fricative]
    symbol ʁ [+voice uvular fricative]
    symbol ħ [-voice pharyngeal fricative]
    symbol ʕ [+voice pharyngeal fricative]
    symbol h [-voice glottal fricative]
    symbol ɦ [+voice glottal fricative]

    symbol m [+voice bilabial nasal]
    symbol ɱ [+voice labiodental nasal]
    symbol n [+voice alveolar nasal]
    symbol ɳ [+voice retroflex nasal]
    symbol ɲ [+voice palatal nasal]
    symbol ŋ [+voice velar nasal]
    symbol ɴ [+voice uvular nasal]

    symbol ʙ [+voice bilabial trill]
    symbol r [+voice alveolar trill]
    symbol ʀ [+voice uvular trill]

    symbol ⱱ [+voice labiodental tap]
    symbol ɾ [+voice alveolar tap]
    symbol ɽ [+voice retroflex tap]

    symbol ʋ [+voice labiodental approximant]
    symbol ɹ [+voice alveolar approximant]
    symbol l [+voice alveolar approximant +lateral]
    symbol ɻ [+voice retroflex approximant]
    symbol ɭ [+voice retroflex approximant +lateral]
    symbol j [+voice palatal approximant]
    symbol ʎ [+voice palatal approximant +lateral]
    symbol ɰ [+voice velar approximant]
    symbol ʟ [+voice velar approximant +lateral]

    #Not strictly correct but close enough
    symbol w [+voice bilabial approximant]
end

diacritics
    diacritic ː [-long] => [+long]
    diacritic ̥◌ [+voice] => [-voice]
    diacritic ʰ [-aspirated] => [+aspirated]
    diacritic ʷ [-labialized] => [+labialized]
    diacritic ˠ [-velarized] => [+velarized]
    diacritic ʲ [-palatalized] => [+palatalized]
    diacritic ˤ [-pharyngealized] => [+pharyngealized]
    diacritic ⁿ [-nasal-release] => [+nasal-release]
    diacritic ' [-stress] => [+stress]
    diacritic ◌̯ [-non-syllabic] => [+non-syllabic]
end

rules
    rule h-loss
        {h l x ʔ} => *
    end

    rule vowel-comb
        a a => [+long] *
        a i => a j
        a u => a o
        i a => j a
        i i => [+long] *
        i u => * [+long]
        u a => w a
        u i => w e
        u u => [+long] *
    end

    rule uvular-vowel
        {i u} => {ɛ o} / [uvular] _
    end

    rule rhotic-uvular
        ʀ => χ
        r => χ / $ _
    end

    rule uvular-fricative
        χ => h
    end

    rule ao-change
        a o => [open-mid back +round +long] *
    end

    rule total-uvular-loss
        q => k
    end

    rule b-changes
        ʙ => β / [vowel] _ [vowel]
        ʙ => r
    end

    rule rhotic
        r => ɹ
    end

    rule plosive-voice
        [plosive] => [+voice] / _ [vowel] h
        [vowel] h => * * / [plosive] _
    end

    rule j-loss
        j => * / ɹ _
        j => * / w _
        j => * / h _
    end

    rule j-transform
        j => ʃ / [plosive] _
    end

    rule word-final-j
        j => ʒ / _ $
    end

    rule h-loss-2
        h => *
    end

    rule palatalization
        {k g n m} => {c ɟ ɲ ɲ} / _ i [vowel]
        p => [+palatalized] / _ i [vowel]
        {f s ʃ} => [+palatalized] / _ i [vowel]
        {f s ʃ} => [+palatalized] / _ [close front -round +long]
        {k g n m} j => {c ɟ ɲ ɲ} *
        {p f s ʃ} j => [+palatalized] *
        i => * / [palatal] _ [vowel]
        i => * / [+palatalized] _ [vowel]
        [close front -round +long] => [-long] / [palatal fricative] _
    end

    rule voicing
        [consonant -voice -palatalized] => [+voice] / _ [+round]
    end

    rule vowel-merge
        a a => [+long] *
        e e => [+long] *
        i i => [+long] *
        o o => [+long] *
        u u => [+long] *
        ɛ ɛ => [+long] *
        ɔ ɔ => [+long] *
    end

    rule palatal-change
        [velar plosive +palatalized] => [-palatalized palatal]
    end

    rule palatal-voice
        [+palatalized +voice] => [-voice]
    end

    rule bv-v
        β => v
    end

    rule voiced-h
        ɦ => h
    end
end